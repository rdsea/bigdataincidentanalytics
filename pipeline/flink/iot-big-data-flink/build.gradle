import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.20'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'java'
    id 'application'
}

group 'io.github.rdsea'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'io.github.rdsea.flink.Main'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    ktlint "com.github.shyiko:ktlint:0.30.0"
    implementation 'org.slf4j:slf4j-simple:1.7.25'
    implementation "org.apache.flink:flink-java:1.7.1"
    implementation "org.apache.flink:flink-streaming-java_2.12:1.7.1"
    implementation "org.fusesource.mqtt-client:mqtt-client:1.14"
    implementation "org.apache.flink:flink-connector-elasticsearch6_2.11:1.7.1"
    implementation 'io.github.microutils:kotlin-logging:1.6.22'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.komamitsu:fluency-core:2.1.0'
    implementation 'org.komamitsu:fluency-fluentd:2.1.0'
    implementation 'io.cloudevents:cloudevents-api:0.2.1'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    //noinspection GroovyAssignabilityCheck
    args "src/**/*.kt", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    //noinspection GroovyAssignabilityCheck
    args "-F", "src/**/*.kt"
}

shadowJar {
    baseName = 'flink'
    transform(AppendingTransformer) {
        resource = 'reference.conf'
    }
}