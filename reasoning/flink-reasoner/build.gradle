plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'java'
    id 'application'
}

group 'io.github.rdsea'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'io.github.rdsea.reasoner.Main'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    ktlint "com.pinterest:ktlint:0.36.0"
    implementation "org.apache.flink:flink-java:1.9.1"
    implementation "org.apache.flink:flink-streaming-java_2.12:1.9.1"
    implementation "org.apache.flink:flink-connector-elasticsearch6_2.11:1.9.1"
    implementation "org.apache.flink:flink-connector-kafka_2.12:1.9.1"
    implementation "org.apache.flink:flink-connector-cassandra_2.12:1.9.1"
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.neo4j.driver:neo4j-java-driver:4.0.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--experimental","src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "--experimental","src/**/*.kt"
}

shadowJar {
    baseName = 'flink-reasoner'
}